PREFIX dct: <http://purl.org/dc/terms/>
PREFIX npo: <http://purl.bioontology.org/ontology/npo#>
PREFIX bfo: <http://www.ifomis.org/bfo/1.1/snap#>
PREFIX dcat: <http://www.w3.org/ns/dcat#>

SELECT ?datasetLabel (COUNT(DISTINCT ?materialQualityType ) AS ?materialQualityTypeCount)
WHERE { 
  
    ?dataset dcat:distribution ?datasetrdf.
    ?datasetrdf rdfs:label ?datasetLabel.

    ?material dct:source ?datasetrdf;
              dct:type ?materialType.
              
    {
        ?material npo:has_quality ?materialQuality.
    }
    UNION
    {
        ?material npo:has_part ?material_part.
        ?material_part npo:has_quality ?materialQuality.
    }
    
    ?materialQuality rdf:type ?materialQualityType.

    FILTER (?materialQualityType NOT IN (bfo:Quality))
} 
GROUP BY ?datasetLabel
ORDER BY ?datasetLabel
